# Sample

#title:Wikipedia sitelinks for Art Looting Investigation Unit Red Flag Names
#defaultView:ScatterChart
SELECT ?lang ?item ?itemLabel ?itemDescription  ?article ?articleLabel WHERE {
  hint:Query hint:optimizer "None".
  ?item wdt:P1840 wd:Q30335959 . #Red Flag names
  ?article schema:about ?item . # find articles about 
  ?article schema:isPartOf / wikibase:wikiGroup "wikipedia" . # only Wikipedias articles
  hint:Prior hint:gearing "forward" .
  # This hint says to search the property chain above from left to right ("forward"),
  # i.e. it is checked if each previously found value for ?article is linked to a Wikipedia.
  # The default ("reverse") would be to search it from right to left, i.e. find all Wikipedia
  # articles first, and then select the intersection with the previously found values of ?article.
  ?article schema:inLanguage ?lang .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
}
# https://w.wiki/6dRR by https://twitter.com/OpenLinkArtData

# Sample

#title:Number of Wikipedia sitelinks for Art Looting Investigation Unit Red Flag Names
SELECT ?lang (COUNT(DISTINCT ?article) AS ?count) WHERE {
  hint:Query hint:optimizer "None".
  ?item wdt:P1840 wd:Q30335959 . #Red Flag names
  ?article schema:about ?item . # find articles about 
  ?article schema:isPartOf / wikibase:wikiGroup "wikipedia" . # only Wikipedias articles
  hint:Prior hint:gearing "forward" .
  # This hint says to search the property chain above from left to right ("forward"),
  # i.e. it is checked if each previously found value for ?article is linked to a Wikipedia.
  # The default ("reverse") would be to search it from right to left, i.e. find all Wikipedia
  # articles first, and then select the intersection with the previously found values of ?article.
  ?article schema:inLanguage ?lang .
  SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],en". }
} GROUP BY ?lang
ORDER BY DESC (?count)

# https://w.wiki/6dQW by https://twitter.com/OpenLinkArtData